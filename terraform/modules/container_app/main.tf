resource "azurerm_container_app" "main" {
  name                         = "kanban-app-${var.env}"
  resource_group_name          = var.resource_group_name
  container_app_environment_id = var.container_app_env_id
  revision_mode                = "Single"
  depends_on = [
    time_sleep.wait_for_secrets_user,
    azurerm_key_vault_secret.jwt_secret,
    azurerm_key_vault_secret.spring_mail_username,
    azurerm_key_vault_secret.spring_mail_password,
    azurerm_key_vault_secret.captcha_secret,
  ]

  secret {
    name                = "postgres-connection-string"
    key_vault_secret_id = format("%s/secrets/%s", trimsuffix(var.key_vault_uri, "/"), "POSTGRES-CONNECTION-STRING")
    identity            = azurerm_user_assigned_identity.main.id
  }
  secret {
    name                = "postgres-user"
    key_vault_secret_id = format("%s/secrets/%s", trimsuffix(var.key_vault_uri, "/"), "POSTGRES-USER")
    identity            = azurerm_user_assigned_identity.main.id
  }
  secret {
    name                = "postgres-password"
    key_vault_secret_id = format("%s/secrets/%s", trimsuffix(var.key_vault_uri, "/"), "POSTGRES-PASSWORD")
    identity            = azurerm_user_assigned_identity.main.id
  }
  secret {
    name                = "jwt-secret-key"
    key_vault_secret_id = format("%s/secrets/%s", trimsuffix(var.key_vault_uri, "/"), "JWT-SECRET-KEY")
    identity            = azurerm_user_assigned_identity.main.id
  }
  secret {
    name                = "spring-mail-username"
    key_vault_secret_id = format("%s/secrets/%s", trimsuffix(var.key_vault_uri, "/"), "SPRING-MAIL-USERNAME")
    identity            = azurerm_user_assigned_identity.main.id
  }
  secret {
    name                = "spring-mail-password"
    key_vault_secret_id = format("%s/secrets/%s", trimsuffix(var.key_vault_uri, "/"), "SPRING-MAIL-PASSWORD")
    identity            = azurerm_user_assigned_identity.main.id
  }
  secret {
    name                = "captcha-secret"
    key_vault_secret_id = format("%s/secrets/%s", trimsuffix(var.key_vault_uri, "/"), "CAPTCHA-SECRET")
    identity            = azurerm_user_assigned_identity.main.id
  }

  template {
    container {
      name   = "kanban-app"
      image  = "ghcr.io/${var.github_repository_owner}/kanbanproject-app:${var.app_image_tag}"
      cpu    = 0.25
      memory = "0.5Gi"

      env {
        name        = "SPRING_DATASOURCE_URL"
        secret_name = "postgres-connection-string"
      }
      env {
        name        = "SPRING_DATASOURCE_USERNAME"
        secret_name = "postgres-user"
      }
      env {
        name        = "SPRING_DATASOURCE_PASSWORD"
        secret_name = "postgres-password"
      }
      env {
        name        = "JWT_SECRET_KEY"
        secret_name = "jwt-secret-key"
      }
      env {
        name        = "SPRING_MAIL_USERNAME"
        secret_name = "spring-mail-username"
      }
      env {
        name        = "SPRING_MAIL_PASSWORD"
        secret_name = "spring-mail-password"
      }
      env {
        name  = "CAPTCHA_ENABLED"
        value = tostring(var.captcha_enabled)
      }
      env {
        name        = "CAPTCHA_SECRET"
        secret_name = "captcha-secret"
      }

      env {
        name  = "VITE_RECAPTCHA_SITE_KEY"
        value = var.vite_recaptcha_site_key
      }
    }

    min_replicas = 1
    max_replicas = 5

    http_scale_rule {
      name                = "http-scale"
      concurrent_requests = "50"
    }

  }


  ingress {
    external_enabled = true
    target_port      = 8080
    transport        = "http"
    traffic_weight {
      percentage      = 100
      latest_revision = true
    }
  }

  identity {
    type         = "UserAssigned"
    identity_ids = [azurerm_user_assigned_identity.main.id]
  }
}

resource "azurerm_user_assigned_identity" "main" {
  name                = "kanban-app-identity-${var.env}"
  location            = var.location
  resource_group_name = var.resource_group_name
}

# Grant the Container App user-assigned identity read access to Key Vault secrets.
# The vault runs in RBAC mode, so this role assignment -- not an access policy -- is
# what actually resolves the key_vault_secret_id references declared above.
resource "azurerm_role_assignment" "key_vault_secrets_user" {
  scope                = var.key_vault_id
  role_definition_name = "Key Vault Secrets User"
  principal_id         = azurerm_user_assigned_identity.main.principal_id
}

# Container Apps resolves every Key Vault secret reference while provisioning the
# first revision. Data-plane RBAC propagates asynchronously, so without this wait
# that revision can fail to start with a 403 on an assignment Azure has accepted.
resource "time_sleep" "wait_for_secrets_user" {
  depends_on      = [azurerm_role_assignment.key_vault_secrets_user]
  create_duration = "60s"
}

resource "azurerm_key_vault_secret" "jwt_secret" {
  name         = "JWT-SECRET-KEY"
  value        = var.jwt_secret_key
  key_vault_id = var.key_vault_id
}

resource "azurerm_key_vault_secret" "spring_mail_username" {
  name         = "SPRING-MAIL-USERNAME"
  value        = var.spring_mail_username
  key_vault_id = var.key_vault_id
}

resource "azurerm_key_vault_secret" "spring_mail_password" {
  name         = "SPRING-MAIL-PASSWORD"
  value        = var.spring_mail_password
  key_vault_id = var.key_vault_id
}

resource "azurerm_key_vault_secret" "captcha_secret" {
  name         = "CAPTCHA-SECRET"
  value        = var.captcha_secret
  key_vault_id = var.key_vault_id
}